num randPrecision = 100;
num goldenAngle = 2.39996;
num pi = 3.14159;

str trunkBlock = "minecraft:stripped_dark_oak_log";

num trunkRadius = 2.5;
num trunkBase = 3;
num length = 15;
num xTranslate = 0;
num zTranslate = 0;
str rootBlock = "minecraft:stripped_dark_oak_log";

num topBranches = 8+randomInt(4);
num topBranchLengthUpwardsMin = 2+randomInt(3);
num topBranchLengthOutwardsMin = 5+randomInt(12);
num topBranchVerticalDirection = 1;
num topBranchLeafRadiusMin = 3;
num topBranchLeafTranslate = 0;
num topBranchLeafTopFlatness = 1;
num topBranchLeafBottomFlatness = 2;
num topBranchLeafDensity = 0.5;
str topBranchLeafStructure = "dark_oak_leaves_clump";
str topBranchBlockX = "minecraft:stripped_dark_oak_log[axis=x]";
str topBranchBlockY = "minecraft:stripped_dark_oak_log[axis=y]";
str topBranchBlockZ = "minecraft:stripped_dark_oak_log[axis=z]";

num sideBranchInterval = 3;
num sideBranchMinHeight = 3+randomInt(3);
num sideBranchLengthMin = 8+randomInt(4);
num sideBranchLengthVariation = 2+randomInt(2);
num sideBranchVerticalVariation = 0;
num sideBranchVerticalDirection = 0.5+randomInt(randPrecision)/randPrecision/4;
num sideBranchLeafRadiusMin = 3;
num sideBranchLeafTranslate = 0;
num sideBranchLeafTopFlatness = 1;
num sideBranchLeafBottomFlatness = 2;
num sideBranchLeafDensity = 0.5;
str sideBranchLeafStructure = "dark_oak_leaves_clump";
str sideBranchBlockX = "minecraft:stripped_dark_oak_log[axis=x]";
str sideBranchBlockY = "minecraft:stripped_dark_oak_log[axis=y]";
str sideBranchBlockZ = "minecraft:stripped_dark_oak_log[axis=z]";

// Generate roots at base of trunk
num roots = 5;
num rootMaxLength = 12;
num rootDroop = 0.05;
num rootVerticalDirection = -0.4-randomInt(randPrecision)/randPrecision*0.3;
num rootAngle = randomInt(randPrecision)/randPrecision*2*pi;

if (getBlock(0,length,0) != "minecraft:air") fail;
if (getBlock(0,-5,0) == "minecraft:air") fail;
if (getBlock(5,-5,0) == "minecraft:air") fail;
if (getBlock(-5,-5,0) == "minecraft:air") fail;
if (getBlock(0,-5,5) == "minecraft:air") fail;
if (getBlock(0,-5,-5) == "minecraft:air") fail;

for (num r = 0; r < roots; r = r + 1) {
    // Branch direction vector
    num dx = sin(rootAngle);
    num dy = rootVerticalDirection;
    num dz = cos(rootAngle);
    // Normalize vector
    num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
    dx = dx/mag;
    dy = dy/mag;
    dz = dz/mag;

    for (num i = 0; i <= rootMaxLength; i = i + 0.5) {
        num cx = i*dx;
        num cy = i*dy+trunkBase;
        num cz = i*dz;

        // Place root block
        str currentBlock = getBlock(cx, cy, cz);
        if (currentBlock == "minecraft:stone" ||
        currentBlock == "minecraft:cobblestone" ||
        currentBlock == "minecraft:mossy_cobblestone" ||
        currentBlock == "minecraft:dirt" ||
        currentBlock == "minecraft:grass_block" ||
        currentBlock == "minecraft:black_concrete_powder" ||
        currentBlock == "minecraft:mud") {
            break;
        } else {
            block(cx, cy, cz, rootBlock);
            block(cx+1, cy, cz, rootBlock);
            block(cx-1, cy, cz, rootBlock);
            block(cx, cy, cz+1, rootBlock);
            block(cx, cy, cz-1, rootBlock);
        }

        // Point vector down more
        dy = dy - rootDroop;
        // Normalize vector
        mag = sqrt(pow2(dx)+pow2(dy)+pow2(dz));
        dx = dx/mag;
        dy = dy/mag;
        dz = dz/mag;
    }

    rootAngle = rootAngle + goldenAngle;
}

num branchAngle = randomInt(randPrecision)/randPrecision*2*pi;

// tree trunk
for(num h = 0; h <= length; h = h + 1) {
    // Reduce radius inversely to length
    num r = (trunkRadius/length * -h/2) + trunkRadius;

    // Generate sphere at each point
    for(num x = -r; x <= r; x = x + 1) {
        for(num y = -r; y <= r; y = y + 1) {
            for(num z = -r; z <= r; z = z + 1) {
                if (pow(x,2)+pow(y,2)+pow(z,2) < pow(r,2)) {
                    block(x + xTranslate, h + y - trunkRadius, z + zTranslate, trunkBlock, false);                  
                }
            }
        }
    }

    if (h > sideBranchMinHeight) {
        if (h % sideBranchInterval == 0) {
            // Branch direction vector
            num dx = sin(branchAngle);
            num dy = sideBranchVerticalDirection + randomInt(randPrecision)/randPrecision*sideBranchVerticalVariation;
            num dz = cos(branchAngle);
            // Normalize vector
            num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
            dx = dx/mag;
            dy = dy/mag;
            dz = dz/mag;
            // Branch origin
            num ox = xTranslate;
            num oy = h;
            num oz = zTranslate;
            // Set branch block based on largest unsigned vector component
            str branchBlock = sideBranchBlockY;
            if (abs(dx) > max(abs(dy),abs(dz))) branchBlock = sideBranchBlockX;
            else if (abs(dz) > max(abs(dy),abs(dx))) branchBlock = sideBranchBlockZ;
            num branchLength = sideBranchLengthMin + randomInt(randPrecision)/randPrecision*sideBranchLengthVariation;
            // Generate branch
            for (num l = 0; l <= branchLength; l = l + 1) {
                block(l*dx+ox, l*dy+oy, l*dz+oz, branchBlock);
                block(l*dx+ox+1, l*dy+oy, l*dz+oz, branchBlock);
                block(l*dx+ox-1, l*dy+oy, l*dz+oz, branchBlock);
                block(l*dx+ox, l*dy+oy, l*dz+oz+1, branchBlock);
                block(l*dx+ox, l*dy+oy, l*dz+oz-1, branchBlock);
            }
            // Rotate angle of next branch
            branchAngle = branchAngle + goldenAngle;
            // Generate leaf cluster at the end of the branch
            num radius = sideBranchLeafRadiusMin;
            num radiusSquared = pow(radius, 2);
            // Leaf cluster origin (end of branch)
            num lox = ox+branchLength*dx;
            num loy = oy+branchLength*dy + sideBranchLeafTranslate;
            num loz = oz+branchLength*dz;
            for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
                for (num cy = ceil(-radius/sideBranchLeafBottomFlatness); cy <= ceil(radius/sideBranchLeafTopFlatness); cy = cy + 1) {
                    for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                        num squashFactor = sideBranchLeafBottomFlatness;
                        if (cy > 0) squashFactor = sideBranchLeafTopFlatness;
                        num distanceSquared = pow(cx,2)+pow(cy*squashFactor,2)+pow(cz,2);
                        if (distanceSquared < radiusSquared && randomInt(randPrecision)/randPrecision < sideBranchLeafDensity) {
                            structure(lox+cx, loy+cy, loz+cz, sideBranchLeafStructure, "NONE", "CW_90", "CCW_90", "CW_180");
                        }
                    }
                }
            }
        }
    }
    xTranslate = xTranslate+(1-randomInt(3));
    zTranslate = zTranslate+(1-randomInt(3));
}
for (num b = 1; b <= topBranches; b = b + 1) {
    num t = b/topBranches;
    num ti = 1-t;
    // Branch direction vector
    num dx = sin(branchAngle) * t;
    num dy = topBranchVerticalDirection;
    num dz = cos(branchAngle) * t;
    // Normalize vector
    num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
    dx = dx/mag;
    dy = dy/mag;
    dz = dz/mag;
    // Branch origin
    num ox = xTranslate;
    num oy = length-3;
    num oz = zTranslate;
    // Set branch block based on largest unsigned vector component
    str branchBlock = topBranchBlockY;
    if (abs(dx) > max(abs(dy),abs(dz))) branchBlock = topBranchBlockX;
    else if (abs(dz) > max(abs(dy),abs(dx))) branchBlock = topBranchBlockZ;
    num branchLength = topBranchLengthUpwardsMin*ti + topBranchLengthOutwardsMin*t;
    // Generate branch
    for (num l = 0; l <= branchLength; l = l + 1) {
        block(l*dx+ox, l*dy+oy, l*dz+oz, branchBlock);
    }
    // Rotate angle of next branch
    branchAngle = branchAngle + goldenAngle;
    // Generate leaf cluster at the end of the branch
    num radius = topBranchLeafRadiusMin;
    num radiusSquared = pow(radius, 2);
    // Leaf cluster origin (end of branch)
    num lox = ox+branchLength*dx;
    num loy = oy+branchLength*dy + topBranchLeafTranslate;
    num loz = oz+branchLength*dz;
    for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
        for (num cy = ceil(-radius/topBranchLeafBottomFlatness); cy <= ceil(radius/topBranchLeafTopFlatness); cy = cy + 1) {
            for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                num squashFactor = topBranchLeafBottomFlatness;
                if (cy > 0) squashFactor = topBranchLeafTopFlatness;
                num distanceSquared = pow(cx,2)+pow(cy*squashFactor,2)+pow(cz,2);
                if (distanceSquared < radiusSquared && randomInt(randPrecision)/randPrecision < topBranchLeafDensity) {
                    structure(lox+cx, loy+cy, loz+cz, topBranchLeafStructure, "NONE", "CW_90", "CCW_90", "CW_180");
                }
            }
        }
    }
}

num twopi = pi*2;
for (num v = 15; v < length+20; v = v + 0.1) {
    num rx = sin(randomInt(randPrecision)/randPrecision * twopi) * (randomInt(7) + 3);
    num rz = cos(randomInt(randPrecision)/randPrecision * twopi) * (randomInt(7) + 3);
    structure(rx, v, rz, "place_vines", "NONE");
    structure(-rx, v, rz, "place_vines", "NONE");
    structure(rx, v, -rz, "place_vines", "NONE");
    structure(-rx, v, -rz, "place_vines", "NONE");
}