num randPrecision = 100;
num radius = 5;
num warp = 5;
num warpFreq = 2;
num squish = 1;
num radiusSquared = pow(radius,2);
num yTranslate = 1;

if (check(radius, -radius/squish, 0) != "LAND" ||
    check(-radius, -radius/squish, 0) != "LAND" ||
    check(0, -radius/squish, radius) != "LAND" ||
    check(0, -radius/squish, -radius) != "LAND"
    ) fail;

for (num x = -radius-warp; x < radius+warp; x = x + 1) {
    for (num y = (-radius-warp)/squish; y < (radius+warp)/squish; y = y + 1) {
        for (num z = -radius-warp; z < radius+warp; z = z + 1) {
            num warpX = warp * sampler("simplex3",
                                warpFreq*(x+originX()),
                                warpFreq*(y+originY()+1000),
                                warpFreq*(z+originZ()));
            num warpY = warp * sampler("simplex3",
                                warpFreq*(x+originX()),
                                warpFreq*(y+originY()+2000),
                                warpFreq*(z+originZ()));
            num warpZ = warp * sampler("simplex3",
                                warpFreq*(x+originX()),
                                warpFreq*(y+originY()+3000),
                                warpFreq*(z+originZ()));
			num distanceSquared = pow(x+warpX,2)+pow((y+warpY)*squish,2)+pow(z+warpZ,2);
			if (distanceSquared < radiusSquared && distanceSquared > radiusSquared-(radius*3)) {
				structure(x, y+yTranslate+radius, z, "bee_nest_block", "NONE");
			}

            if (distanceSquared < (radiusSquared-(radius*3)) && y < (0)) {
				block(x, y+yTranslate+radius, z, "minecraft:honey_block", false);
			}

            distanceSquared = pow((x+warpX)*1.5,2)+pow((y+warpY)*squish,2)+pow((z+warpZ)*1.5,2);
            if (distanceSquared < (radiusSquared)) {
				structure(x, y+yTranslate, z, "bee_nest_block", "NONE");
			}
        }
    }
}



for (num i = -5; i <= 5; i = i + 10) {
    if (getBlock(i,0,0) == "minecraft:air") entity(i, 0, 0, "minecraft:bee");
    if (getBlock(0,0,i) == "minecraft:air") entity(0, 0, i, "minecraft:bee");
    if (getBlock(i,0,i) == "minecraft:air") entity(i, 0, i, "minecraft:bee");
}   