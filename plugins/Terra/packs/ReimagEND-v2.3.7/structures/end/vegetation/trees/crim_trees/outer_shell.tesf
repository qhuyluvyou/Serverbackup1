num randPrecision = 100;
num goldenAngle = 2.39996;
num pi = 3.14159;

num amount = 5+randomInt(5);

num radius = 1;

str mainBlock = "minecraft:purple_terracotta";
str altBlock = "minecraft:crimson_planks";

structure(0,0,0,"inner_shell","NONE");

str setBlock = "";
for (num a = 0; a < amount; a = a + 1) {

    num length = 5+randomInt(7);
    
    num droop = -0.03;
    num verticalDirection = -2-randomInt(randPrecision)/randPrecision;
    num angle = randomInt(randPrecision)/randPrecision*2*pi;

    // Branch direction vector
    num dx = sin(angle);
    num dy = verticalDirection;
    num dz = cos(angle);
    // Normalize vector
    num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
    dx = dx/mag;
    dy = dy/mag;
    dz = dz/mag;

    for (num l = 0; l <= length; l = l + 0.1) {

        num cx = l*dx;
        num cy = l*dy;
        num cz = l*dz;
        
        num r = radius;
        for(num x = -r; x <= r; x = x + 1) {
            for(num y = -r; y <= r; y = y + 1) {
                for(num z = -r; z <= r; z = z + 1) {
                    setBlock = mainBlock;
                    if (randomInt(2) == 0) setBlock = altBlock;
                        
                    if (pow(x,2)+pow(y,2)+pow(z,2) < pow(r,2)) {
                        block(cx + x, cy + y, cz + z, setBlock, false); 
                    }
                }  
            }
        }

        // Point vector down more
        dy = dy - droop;
        // Normalize vector
        mag = sqrt(pow2(dx)+pow2(dy)+pow2(dz));
        dx = dx/mag;
        dy = dy/mag;
        dz = dz/mag;
    }

    angle = angle + goldenAngle;
}


