num randPrecision = 100;
num goldenAngle = 2.39996;
num pi = 3.14159;
num twopi = 6.28318530718;

num spikes = 8;

num angle = randomInt(randPrecision)/randPrecision*2*pi;

str setBlock = "minecraft:obsidian";

for (num a = 0; a < spikes; a = a + 1) {

    num length = 25 + randomInt(randPrecision)/randPrecision*5;
    num radius = 4 + randomInt(randPrecision)/randPrecision;

    num dx = sin(angle);
    num dy = 1.5;
    num dz = cos(angle);
    
    // Normalize vector
    num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
    dx = dx/mag;
    dy = dy/mag;
    dz = dz/mag;

    for(num l = 0; l <= length; l = l + 1) {

        // Reduce radius inversely to length
        num r = (radius/length * -l) + radius;

        // Generate sphere at each point
        for(num x = -r; x <= r; x = x + 1) {
            for(num y = -r; y <= r; y = y + 1) {
                for(num z = -r; z <= r; z = z + 1) {
                    if (pow(x,2)+pow(y,2)+pow(z,2) < pow(r,2)) {
                            setBlock = "minecraft:obsidian";
                                if (randomInt(6) == 0) setBlock = "minecraft:crying_obsidian";
                                block(l*dx + x, l*dy + y - radius, l*dz + z, setBlock, false);
                        }
                }
            }
        }
    }

    angle = angle + goldenAngle;
}
